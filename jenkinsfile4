pipeline {
    agent any
    environment {
        BLUE_SERVER = "54.242.239.106"
        GREEN_SERVER = "98.93.30.214"
        REMOTE_PATH = "/var/www/html/index.html"
        LOCAL_WORKSPACE_FILE = "${env.WORKSPACE}/index.html"

        // ALB Listener ARN for HTTP 80
        ALB_LISTENER_ARN = "arn:aws:elasticloadbalancing:us-east-1:894103237522:listener/app/alb-bg/da7aa830e5c34bb8/339494166"

        BLUE_TG_ARN = "arn:aws:elasticloadbalancing:us-east-1:894103237522:targetgroup/blue-tg/ce86ede8e7af1d3b"
        GREEN_TG_ARN = "arn:aws:elasticloadbalancing:us-east-1:894103237522:targetgroup/green-tg/8fb6bf5cde4f6043"
    }
    stages {
        stage('Checkout SCM') {
            steps {
                echo "Checking out source code..."
                git(
                    url: 'https://github.com/Zeeshancloud15/jenkins-ecr.git',
                    credentialsId: 'blue-green-ssh',
                    branch: 'main'
                )
            }
        }

        stage('Prepare index.html') {
            steps {
                echo "Pulling index.html from BLUE server..."
                sshagent(['blue-green-ssh']) {
                    sh "scp -o StrictHostKeyChecking=no jenkins@${BLUE_SERVER}:${REMOTE_PATH} ${LOCAL_WORKSPACE_FILE}"
                }
            }
        }

        stage('Deploy to GREEN') {
            steps {
                echo "Deploying index.html to GREEN server..."
                sshagent(['blue-green-ssh']) {
                    sh "scp -o StrictHostKeyChecking=no ${LOCAL_WORKSPACE_FILE} jenkins@${GREEN_SERVER}:${REMOTE_PATH}"
                }
            }
        }

        stage('Health Check') {
            steps {
                echo "Checking GREEN server health..."
                sshagent(['blue-green-ssh']) {
                    script {
                        def status = sh(
                            script: "ssh -o StrictHostKeyChecking=no jenkins@${GREEN_SERVER} curl -s -o /dev/null -w '%{http_code}' http://localhost/index.html",
                            returnStdout: true
                        ).trim()
                        echo "HTTP Status: ${status}"
                        if (status != "200") {
                            error "GREEN server health check failed!"
                        }
                    }
                }
            }
        }

        stage('Switch Traffic to GREEN') {
            steps {
                echo "Switching traffic to GREEN target group..."
                sh """
                aws elbv2 modify-listener --listener-arn ${ALB_LISTENER_ARN} --default-actions Type=forward,TargetGroupArn=${GREEN_TG_ARN}
                """
            }
        }
    }

    post {
        failure {
            echo "Deployment failed! Rolling back to BLUE target group..."
            sh """
            aws elbv2 modify-listener --listener-arn ${ALB_LISTENER_ARN} --default-actions Type=forward,TargetGroupArn=${BLUE_TG_ARN}
            """
        }
    }
}
